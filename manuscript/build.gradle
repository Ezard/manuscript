import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.vanniktech.maven.publish'
}

android {
    namespace 'io.ezard.manuscript'
    compileSdk 33

    defaultConfig {
        minSdk 23
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += [
                '-Xcontext-receivers'
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.0'
    }

    publishing {
        multipleVariants {
            allVariants()
        }
    }
}

static def getVersion() {
    // Unix
    def result = "git describe --tags --abbrev=0".execute().text.trim()
    // Windows
    if (result.empty) result = "PowerShell -Command git describe --tags --abbrev=0".execute().text.trim()
    // Git isn't installed, or there are no tags
    if (result.empty) result = "0.0.0"
    return result
}

mavenPublishing {
    coordinates("io.ezard.manuscript", "manuscript", getVersion())
    publishToMavenCentral(SonatypeHost.S01)
    signAllPublications()

    pom {
        name = "Manuscript"
        description = "Component library creator for Jetpack Compose"
        inceptionYear = "2022"
        url = "https://github.com/Ezard/manuscript"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "ezard"
                name = "Ben Ezard"
                url = "https://github.com/Ezard"
            }
        }
        scm {
            url = "https://github.com/Ezard/manuscript"
            connection = "scm:git:git://github.com/Ezard/manuscript.git"
            developerConnection = "scm:git:ssh://git@github.com/Ezard/manuscript.git"
        }
    }
}

dependencies {
    implementation 'androidx.activity:activity-compose:1.7.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.2'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_material_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.activity:activity-ktx:$activity_ktx_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
}
