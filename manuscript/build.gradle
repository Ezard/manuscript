import org.jetbrains.dokka.gradle.DokkaTask

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.dokka'
    id 'com.vanniktech.maven.publish'
}

android {
    namespace 'io.ezard.manuscript'
    compileSdk 33

    defaultConfig {
        minSdk 23
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += [
                '-Xcontext-receivers'
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "${libs.versions.composeCompiler}"
    }

    publishing {
        multipleVariants {
            allVariants()
        }
    }
}

dependencies {
    api project(':annotations')
    ksp project(':ksp')
    implementation(libs.androidx.activity.compose)
    implementation(libs.androidx.activity.ktx)
    coreLibraryDesugaring(libs.android.tools.desugar)
    implementation(libs.compose.ui)
    implementation(libs.compose.material)
    implementation(libs.compose.material.icons)
    implementation(libs.compose.tooling)
    implementation(libs.compose.tooling.preview)
}

tasks.withType(DokkaTask.class) {
    moduleName.set("Manuscript")
    moduleVersion.set(getVersion())
    outputDirectory.set(file("docs"))
}
